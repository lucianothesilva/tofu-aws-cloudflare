provider "aws" {
  region = var.region
}

provider "cloudflare" {
  api_key = var.cloudflare_api_key
  email   = var.cloudflare_email
}

resource "aws_s3_bucket" "s3_bucket" {
  bucket = var.s3_bucket_name
  tags = {
    Name        = var.s3_bucket_name
    Environment = "Production"
  }
}

# Certificado SSL
resource "aws_acm_certificate" "my_cert" {
  domain_name       = var.domain_name
  validation_method = "DNS"
}

locals {
  domain_validation_options = tolist(aws_acm_certificate.my_cert.domain_validation_options)
  origin_id                 = "${var.s3_bucket_name}.${var.region}"
}

resource "cloudflare_record" "cert_validation" {
  count   = length(local.domain_validation_options)
  zone_id = var.cloudflare_zone_id
  name    = local.domain_validation_options[count.index].resource_record_name
  value   = local.domain_validation_options[count.index].resource_record_value
  type    = local.domain_validation_options[count.index].resource_record_type
  ttl     = 60
}

resource "aws_acm_certificate_validation" "my_cert_validation" {
  certificate_arn         = aws_acm_certificate.my_cert.arn
  validation_record_fqdns = [for record in cloudflare_record.cert_validation : record.hostname]
}

# CloudFront
resource "aws_cloudfront_origin_access_identity" "frontend_oai" {}

resource "aws_cloudfront_distribution" "cf_distribution" {
  origin {
    domain_name = aws_s3_bucket.s3_bucket.bucket_domain_name
    origin_id   = local.origin_id

    s3_origin_config {
      origin_access_identity = aws_cloudfront_origin_access_identity.frontend_oai.cloudfront_access_identity_path
    }
  }

  enabled             = true
  is_ipv6_enabled     = true
  comment             = var.cloudfront_comment
  default_root_object = "index.html"

  aliases = [var.domain_name]

  custom_error_response {
    error_caching_min_ttl = 300
    error_code            = 403
    response_page_path    = "/index.html"
    response_code         = 200
  }

  default_cache_behavior {
    allowed_methods        = ["GET", "HEAD"]
    cached_methods         = ["GET", "HEAD"]
    target_origin_id       = local.origin_id
    viewer_protocol_policy = "redirect-to-https"
    min_ttl                = 0
    default_ttl            = 3600
    max_ttl                = 86400
    compress               = true

    cache_policy_id          = "658327ea-f89d-4fab-a63d-7e88639e58f6" # CachingOptimized
    origin_request_policy_id = null
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    acm_certificate_arn      = aws_acm_certificate.my_cert.arn
    ssl_support_method       = "sni-only"
    minimum_protocol_version = "TLSv1.2_2021"
  }
}

resource "aws_iam_policy" "cloudfront_policy" {
  name        = "cloudfront_policy"
  description = "Policy for CloudFront invalidation"
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Sid    = "VisualEditor0",
        Effect = "Allow",
        Action = [
          "cloudfront:GetInvalidation",
          "cloudfront:CreateInvalidation"
        ],
        Resource = "arn:aws:cloudfront::${data.aws_caller_identity.current.account_id}:distribution/${aws_cloudfront_distribution.cf_distribution.id}"
      }
    ]
  })
}

resource "aws_iam_policy" "s3_policy" {
  name        = var.s3_policy_name
  description = "Policy for S3 access"
  policy = jsonencode({
    Version = "2012-10-17",
    Statement = [
      {
        Effect = "Allow",
        Action = [
          "s3:PutObject",
          "s3:GetObject",
          "s3:ListBucket"
        ],
        Resource = [
          "arn:aws:s3:::${aws_s3_bucket.s3_bucket.bucket}/*",
          "arn:aws:s3:::${aws_s3_bucket.s3_bucket.bucket}"
        ]
      }
    ]
  })
}

resource "aws_s3_bucket_policy" "s3_bucket_policy" {
  bucket = aws_s3_bucket.s3_bucket.id

  policy = jsonencode({
    Version = "2008-10-17",
    Id      = "PolicyForCloudFrontPrivateContent",
    Statement = [
      {
        Sid    = "1",
        Effect = "Allow",
        Principal = {
          AWS = aws_cloudfront_origin_access_identity.frontend_oai.iam_arn
        },
        Action   = "s3:GetObject",
        Resource = "${aws_s3_bucket.s3_bucket.arn}/*"
      }
    ]
  })
}

resource "aws_iam_policy_attachment" "attach_cloudfront_policy" {
  name       = "attach_cloudfront_policy"
  policy_arn = aws_iam_policy.cloudfront_policy.arn
  groups     = [aws_iam_group.frontend_group.name]
}

resource "aws_iam_policy_attachment" "attach_s3_policy" {
  name       = "attach_s3_policy"
  policy_arn = aws_iam_policy.s3_policy.arn
  groups     = [aws_iam_group.frontend_group.name]
}

data "aws_caller_identity" "current" {}

resource "aws_iam_group" "frontend_group" {
  name = var.group_name
}

resource "aws_iam_user" "frontend_user" {
  name = var.user_name
}

resource "aws_iam_group_membership" "frontend_group_membership" {
  name  = "frontend_group_membership"
  users = [aws_iam_user.frontend_user.name]
  group = aws_iam_group.frontend_group.name
}
resource "cloudflare_record" "cname_record" {
  zone_id = var.cloudflare_zone_id
  name    = var.domain_name
  value   = aws_cloudfront_distribution.cf_distribution.domain_name
  type    = "CNAME"
  ttl     = 300
}
